#include <Arduino.h>
#include <PS4Controller.h>

#define MIN_MOTOR_VALUE 153 // (0 - 255) With no load, 100RPM motor requires ~60% power to begin rotating. .6 * 255 = 153 (round up) Value may need to be set higher with load.
#define MAX_MOTOR_VALUE 255 // (0 - 255) Speed limit

#define DEADZONE_SIZE 15

#define MOTOR_A_DIRECTION 1 // Set to -1 to reverse motor direction, instead of rewiring
#define MOTOR_B_DIRECTION 1 

#define ENABLE_A_PIN 14 // PWM pin for Motor A (left motor)
#define IN1_PIN 27 // Direction pin 1 for Motor A (left motor)
#define IN2_PIN 26 // Direction pin 2 for Motor A (left motor)
#define IN3_PIN 25 // Direction pin 1 for Motor B (right motor)
#define IN4_PIN 33 // Direction pin 2 for Motor B (right motor)
#define ENABLE_B_PIN 32 // PWM pin for Motor B (right motor)

#define LED_CHANNEL_A 0
#define LED_CHANNEL_B 1

int leftStickY;
int leftPWMOutput;

int rightStickY;
int rightPWMOutput;

// Get pinout and pinin values 
void setup()
{
  Serial.begin(115200);
  PS4.begin("01:02:03:04:05:06");
  Serial.println("Waiting for controller to connect to 01:02:03:04:05:06");

  pinMode(IN1_PIN, OUTPUT);
  pinMode(IN2_PIN, OUTPUT);
  pinMode(IN3_PIN, OUTPUT);
  pinMode(IN4_PIN, OUTPUT);

  /* 
  The ESP32 doesn't use "analogWrite()" like most basic Arduino boards.
  Instead, you get several PWM channels, and can tie them to (almost)
  any pin you choose. Even though this was originally meant for LEDs,
  it works fine with motor driver boards.
  */

  ledcSetup(LED_CHANNEL_A, 30000, 8);
  ledcAttachPin(ENABLE_A_PIN, LED_CHANNEL_A);
  
  ledcSetup(LED_CHANNEL_B, 30000, 8);
  ledcAttachPin(ENABLE_B_PIN, LED_CHANNEL_B);
}

void loop()
{
  if(PS4.isConnected())
  {
    // Get raw y values from both input sticks
    leftStickY = PS4.data.analog.stick.ly;
    rightStickY = PS4.data.analog.stick.ry;
    
    Serial.print("LStick: ");
    Serial.print(leftStickY, DEC);
    Serial.print("\t RStick: ");
    Serial.print(rightStickY, DEC);

    // Prevent values from being -128
    if (leftStickY < 0)
      leftStickY = leftStickY + 1;

    if (rightStickY < 0)
      rightStickY = rightStickY + 1; 

    // Establish motor directions
    // "* MOTOR_X_DIRECTION" allows motor to be reversed through code instead of wiring
    if (leftStickY * MOTOR_A_DIRECTION < 0)
    {
      digitalWrite(IN1_PIN, HIGH); // Direction A
      digitalWrite(IN2_PIN, LOW);
    }
    else
    {
      digitalWrite(IN1_PIN, LOW); // Direction B
      digitalWrite(IN2_PIN, HIGH);
    }

    if (rightStickY * MOTOR_B_DIRECTION < 0) 
    {
      digitalWrite(IN3_PIN, HIGH); // Direction A
      digitalWrite(IN4_PIN, LOW);
    }
    else
    {
      digitalWrite(IN3_PIN, LOW); // Direction B
      digitalWrite(IN4_PIN, HIGH);
    }
    
    // Convert stick position to magnitude
    leftStickY = abs(leftStickY);
    rightStickY = abs(rightStickY);

    // Map stick positions to usable duty cycles
    leftPWMOutput = map(leftStickY, DEADZONE_SIZE, 127, MIN_MOTOR_VALUE, MAX_MOTOR_VALUE);
    rightPWMOutput = map(rightStickY, DEADZONE_SIZE, 127, MIN_MOTOR_VALUE, MAX_MOTOR_VALUE);

    // Overwrite if stick is within deadzone
    if (leftStickY < DEADZONE_SIZE)
      leftPWMOutput = 0;

    if (rightStickY < DEADZONE_SIZE)
      rightPWMOutput = 0;

    // Establish PWM duty cycles (motor speeds)
    ledcWrite(LED_CHANNEL_A, leftPWMOutput);
    ledcWrite(LED_CHANNEL_B, rightPWMOutput);

    Serial.print("\tLeftPWM:\t");
    Serial.print(leftPWMOutput);
    Serial.print("\tRightPWM:\t");
    Serial.print(rightPWMOutput);
  }
  else
  {
    Serial.println("Controller not connected...");
    delay(1000);
  }
  
  delay(100);
}